apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// https://github.com/bintray/gradle-bintray-plugin/issues/74#issuecomment-168367341
// https://github.com/dcendents/android-maven-gradle-plugin/issues/9
ext {

    GROUP = 'com.codepath.libraries'
    BASE_VERSION = "2.0"
    VERSION_NAME = "2.1.3"
    POM_PACKAGING = "aar"
    POM_DESCRIPTION = "CodePath OAuth Handler"

    POM_ARTIFACT_ID = "android-oauth-handler"
    POM_NAME = "CodePath OAuth Handler"
    POM_URL = "https://github.com/codepath/android-oauth-handler/"
    POM_SCM_URL = "https://github.com/codepath/android-oauth-handler/"
    POM_SCM_CONNECTION = "scm:git:https://github.com/codepath/android-oauth-handler.git"
    POM_SCM_DEV_CONNECTION = "scm:git:git@github.com:codepath/android-oauth-handler.git"

    POM_LICENCE_NAME = "The Apache Software License, Version 2.0"
    POM_LICENCE_URL = "http://www.apache.org/licenses/LICENSE-2.0.txt"
    POM_LICENCE_DIST = "repo"

    POM_DEVELOPER_ID = "codepath"
    POM_DEVELOPER_NAME = "CodePath, Inc."
}

version = VERSION_NAME
group = GROUP
archivesBaseName = POM_ARTIFACT_ID

android {
    compileSdkVersion 28

    defaultConfig {
        versionCode 1
        versionName VERSION_NAME
        minSdkVersion 21
        targetSdkVersion 28
    }

    // Related to https://github.com/scribejava/scribejava/issues/480
    // Scribe expects Java 7 or this custom Apache library
    lintOptions {
        lintConfig rootProject.file('gradle/lint.xml')
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']  // needs apply plugin 'com.jfrog.bintray' to work

    pkg {
        repo = 'maven'
        name = 'android-oauth-handler'
        userOrg = 'codepath'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/codepath/android-oauth-handler.git'
        version {
            name = BASE_VERSION
            desc = POM_NAME
            released = new Date()
            vcsTag = VERSION_NAME
        }
    }

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

artifacts {
    archives sourcesJar
}

ext {
  supportVersion = '28.0.0'
}

dependencies {
    api "androidx.appcompat:appcompat:1.0.2"
    api 'com.codepath.libraries:asynchttpclient:0.0.8'
    api 'com.github.scribejava:scribejava-apis:4.1.1'
    api 'com.github.scribejava:scribejava-httpclient-okhttp:4.1.1'
    implementation 'se.akerfeldt:okhttp-signpost:1.1.0'
    implementation 'oauth.signpost:signpost-core:1.2.1.2'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    implementation "com.squareup.okhttp3:logging-interceptor:4.1.0"

}

/*task jar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}*/
