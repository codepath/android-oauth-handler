apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {

    GROUP = 'com.codepath.libraries'
    BASE_VERSION = "2.3"
    VERSION_NAME = "2.3.1"
    POM_PACKAGING = "aar"
    POM_DESCRIPTION = "CodePath OAuth Handler"

    POM_ARTIFACT_ID = "android-oauth-handler"
    POM_NAME = "CodePath OAuth Handler"
    POM_URL = "https://github.com/codepath/android-oauth-handler/"
    POM_SCM_URL = "https://github.com/codepath/android-oauth-handler/"
    POM_SCM_CONNECTION = "scm:git:https://github.com/codepath/android-oauth-handler.git"
    POM_SCM_DEV_CONNECTION = "scm:git:git@github.com:codepath/android-oauth-handler.git"

    POM_LICENCE_NAME = "The Apache Software License, Version 2.0"
    POM_LICENCE_URL = "http://www.apache.org/licenses/LICENSE-2.0.txt"
    POM_LICENCE_DIST = "repo"

    POM_DEVELOPER_ID = "codepath"
    POM_DEVELOPER_NAME = "CodePath, Inc."
}

version = VERSION_NAME
group = GROUP
archivesBaseName = POM_ARTIFACT_ID

android {
    compileSdkVersion 30

    defaultConfig {
        versionCode 1
        versionName VERSION_NAME
        minSdkVersion 21
        targetSdkVersion 30
    }

    // Related to https://github.com/scribejava/scribejava/issues/480
    // Scribe expects Java 7 or this custom Apache library
    lintOptions {
        lintConfig rootProject.file('gradle/lint.xml')
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifact androidSourcesJar

                // You can then customize attributes of the publication as shown below.
                groupId = GROUP
                artifactId = POM_ARTIFACT_ID
                version = VERSION_NAME

                pom {
                    name = POM_NAME
                    url = POM_URL
                    description = POM_DESCRIPTION
                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                    }
                }
            }
        }
        repositories {
            maven {
                name = "Sonatype"
                credentials {
                    username = System.getenv('NEXUS_USERNAME')
                    password = System.getenv('NEXUS_PASSWORD')
                }
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                setUrl(url)
            }
        }
    }
}

signing {
    // gpg on MacOS is the same as gpg2
    // ln -s /usr/local/bin/gpg /usr/local/bin/gpg2
    // Make sure to populate the variables in gradle.properties
    // signing.gnupg.keyName=XXX
    // signing.gnupg.passpharse
    useGpgCmd()
    sign(publishing.publications)
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

artifacts {
    archives sourcesJar
}

ext {
    supportVersion = '28.0.0'
}

dependencies {
    api "androidx.appcompat:appcompat:1.3.0"
    api 'com.codepath.libraries:asynchttpclient:2.1.1'
    api 'com.github.scribejava:scribejava-apis:4.1.1'
    api 'com.github.scribejava:scribejava-httpclient-okhttp:4.1.1'
    implementation 'se.akerfeldt:okhttp-signpost:1.1.0'
    implementation 'oauth.signpost:signpost-core:1.2.1.2'
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'
    implementation "com.squareup.okhttp3:logging-interceptor:4.7.2"

}

task jar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}
